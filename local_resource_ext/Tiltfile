load('../tilt_env/Tiltfile', 'tilt_env_get_or_random', 'tilt_env_set', 'tilt_env_delete')
load('ext://local_output', 'local_output')

def local_resource_ext(name, cmd, on_destroy, **kwargs):
  if config.tilt_subcommand == 'down':
    on_destroy()
  else:
    local_resource(name, cmd, **kwargs)

def s3_bucket(resource_name):
  bucket_prefix = os.getenv('TEST_BUCKET_PREFIX', '')
  bucket_name = tilt_env_get_or_random(resource_name, val_prefix=bucket_prefix)

  def destroy_bucket():
    print("Deleting s3 bucket %s" % bucket_name)
    local('aws s3 rb s3://%s --force;' % bucket_name)
    tilt_env_delete(resource_name)

  print("Registering s3 bucket %s under name %s" % (bucket_name, resource_name))

  local_resource_ext(
    resource_name, 
    'aws s3api head-bucket --bucket %s || aws s3api create-bucket --bucket "%s" --region "$(aws region)" --create-bucket-configuration LocationConstraint="$(aws region)"' % (bucket_name, bucket_name),
    on_destroy=destroy_bucket
  )

  return bucket_name

def credstash_key(resource_name, val):  
  key_prefix = os.getenv('TEST_CREDSTASH_PREFIX', '')
  key_name = tilt_env_get_or_random(resource_name, val_prefix=key_prefix)

  def destroy_key():
    local('credstash delete %s' % key_name)
    tilt_env_delete(resource_name)

  print("Registering credstash key %s under name %s" % (key_name, resource_name))

  local_resource_ext(
    resource_name, 
    'credstash put -a %s %s' % (key_name, val),
    on_destroy=destroy_key
  )

  return key_name